BasicGame
    SpriteSet
        water > Immovable
        mud > Immovable
        fire > Immovable

        key > Resource shrinkfactor=0.5
            redkey > img=oryx/potion3
            bluekey > img=oryx/potion1
            greenkey > img=oryx/potion4
            yellowkey > img=oryx/potion5

        door > Immovable
            reddoor > Immovable img=oryx/slime3
            bluedoor > Immovable img=oryx/slime1
            greendoor > Immovable img=oryx/slime2
            yellowdoor > Immovable img=oryx/slime6

        boots > Resource limit=1
            flippers > img=oryx/boots3
            fireboots > img=oryx/boots2
        
        crate > Passive img=newset/block3
        
        gate > Immovable img=oryx/doorclosed1
        exit > Immovable img=newset/exit2
        chip > Resource img=oryx/gold1 shrinkfactor=0.5 limit=11
        avatar > MovingAvatar img=oryx/rogue
        wall > Immovable img=oryx/wall3 autotiling=True
            
    
    LevelMapping
        w > wall
        A > avatar 
        ~ > water 
        m > mud 
        x > fire 
        r > redkey 
        g > greenkey 
        b > bluekey 
        y > yellowkey 

        1 > reddoor 
        2 > greendoor 
        3 > bluedoor 
        4 > yellowdoor 

        - > gate 
        c > chip 
        e > exit 
        f > flippers 
        i > fireboots 
        d > crate 

    TerminationSet
        SpriteCounter      stype=avatar               limit=0 win=False  
        SpriteCounter      stype=exit               limit=0 win=True        

    InteractionSet
        crate avatar > bounceForward

        water crate > transformTo stype=mud scoreChange=1 killSecond=true
        
        mud avatar > killSprite
        
        gate avatar > killIfOtherHasMore resource=chip limit=11 scoreChange=4
        avatar gate wall > stepBack

        chip avatar > collectResource scoreChange=1
        key avatar > collectResource
        boots avatar > collectResource scoreChange=3

        reddoor avatar > killIfOtherHasMore resource=redkey limit=1 scoreChange=2
        avatar reddoor > changeResource resource=redkey value=-1

        greendoor avatar > killIfOtherHasMore resource=greenkey limit=1  scoreChange=2
        avatar greendoor > changeResource resource=greenkey value=-1

        bluedoor avatar > killIfOtherHasMore resource=bluekey limit=1  scoreChange=2
        avatar bluedoor > changeResource resource=bluekey value=-1

        yellowdoor avatar > killIfOtherHasMore resource=yellowkey limit=1  scoreChange=2
        avatar yellowdoor > changeResource resource=yellowkey value=-1
        
        avatar water > killIfHasLess resource=flippers limit=0
        avatar fire > killIfHasLess resource=fireboots limit=0

        exit avatar > killSprite scoreChange=5